##########################
# Symmetry configuration #
##########################

"""
    SymmetryConfig(particle_symmetry, spin_symmetry; cell_width=1, filling=nothing)

Represents the symmetry configuration of a lattice system, including particle and spin symmetries,
unit cell width, and optional filling information.

# Fields
- `particle_symmetry` : Union{Type{Trivial}, Type{U1Irrep}, Type{SU2Irrep}}
    - The symmetry type for particle number. Use `Trivial` for no symmetry, `U1Irrep` for U(1) symmetry, or `SU2Irrep` for SU(2) symmetry.
- `spin_symmetry` : Union{Type{Trivial}, Type{U1Irrep}, Type{SU2Irrep}}
    - The symmetry type for spin degrees of freedom.
- `cell_width` : Int64
    - Number of sites in the unit cell. Must be a positive integer. Defaults to 1.
- `filling` : Union{Nothing, Rational{Int}}
    - Optional particle filling specified as a rational number `P//Q` (numerator/denominator). Only allowed if `particle_symmetry` is `U1Irrep`.
    Otherwise the filling is determined by the chemical potential.

# Constructor Behavior
- If `filling` is provided, the constructor checks that `particle_symmetry == U1Irrep`.
- `cell_width` must be positive.
- If `particle_symmetry` is `U1Irrep` and `filling` is specified, the constructor ensures that `cell_width` is a multiple of
  `denominator(filling) * (mod(numerator(filling), 2) + 1)` to accommodate the specified filling.
"""
struct SymmetryConfig
    particle_symmetry::Union{Type{Trivial},Type{U1Irrep},Type{SU2Irrep}}
    spin_symmetry::Union{Type{Trivial},Type{U1Irrep},Type{SU2Irrep}}
    cell_width::Int64
    filling::Union{Nothing,Rational{Int}}

    function SymmetryConfig(
                particle_symmetry::Union{Type{Trivial},Type{U1Irrep},Type{SU2Irrep}},
                spin_symmetry::Union{Type{Trivial},Type{U1Irrep},Type{SU2Irrep}},
                cell_width::Int64,
                filling::Union{Nothing,Rational{Int}}=nothing
            )
        @assert cell_width > 0 "Cell width must be a positive integer"

        if particle_symmetry == U1Irrep
            filling = filling === nothing ? 1//1 : filling
            n = numerator(filling)
            d = denominator(filling)
            @assert n > 0 && d > 0 "Filling numerator and denominator must be positive integers"
            necessary_width = d * (mod(n, 2) + 1)
            @assert cell_width % necessary_width == 0 "Cell width ($cell_width) must be a multiple of $necessary_width 
                                                       to accommodate the specified filling ($n / $d)"
        elseif filling !== nothing
            error("Filling can only be specified when particle symmetry is U1Irrep, but got $(particle_symmetry).")
        end

        new(particle_symmetry, spin_symmetry, cell_width, filling)
    end
end


#################
# Hubbard model #
#################

# Convert hopping matrix to dictionary representation
function hopping_matrix2dict(t::Union{Vector{T}, Matrix{T}}) where {T<:AbstractFloat}
    hopping = Dict{NTuple{2,Int},T}()
    bands = isa(t, Matrix) ? size(t,1) : 1
    num_sites = isa(t, Matrix) ? size(t,2) ÷ bands : length(t)

    @assert (isa(t, Matrix) && size(t,1) == bands) || isa(t, Vector) "First dimension of t ($(size(t,1))) must be equal to number of bands ($bands)"
    @assert (isa(t, Matrix) && size(t,2) % bands == 0) || isa(t, Vector) "Second dimension of t ($(size(t,2))) must be a multiple of number of bands ($bands)"
    @assert ishermitian(t[1:bands, 1:bands]) "t on-site matrix is not Hermitian"

    for i in 1:bands
        for j in 1:(num_sites*bands)
            if isa(t, Matrix)
                if t[i, j] != 0.0
                    hopping[(i, j)] = t[i, j]
                    hopping[(j, i)] = conj(t[i, j])
                end
            else
                if t[j] != 0.0
                    hopping[(1,j)] = t[j]
                    hopping[(j,1)] = conj(t[j])
                end
            end
        end
    end

    return hopping
end
# Add 2-body interaction term and its Hermitian conjugate
function addU!(U::Dict{NTuple{4,Int},T}, key::NTuple{4,Int}, val::T) where {T}
    if val != 0
        U[key] = val
        i,j,k,l = key
        U[(l,k,j,i)] = conj(val)          # Hermitian conjugate term
    end
end

"""
    HubbardParams{T<:AbstractFloat}

Represents the standard Hubbard Hamiltonian parameters for a lattice or
multi-orbital system.

# Fields
- `bands::Int64`  
    Number of electronic orbitals or bands per unit cell. Must be positive.
- `t::Dict{NTuple{2, Int64}, T}`  
    Hopping amplitudes. Convention: `t[(i,i)] = μ_i` is the on-site potential,
    `t[(i,j)]` for `i ≠ j` is the hopping amplitude from site i to j.
- `U::Dict{NTuple{4, Int64}, T}`  
    Two-body electronic interaction tensor. Entries `U[(i,j,k,l)]` correspond
    to the operator c⁺_i c⁺_j c_k c_l. Zero entries can be omitted.

# Constructors
- `HubbardParams(bands, t::Dict, U::Dict)` — standard constructor specifying bands, hopping, and interactions.
- `HubbardParams(t::Vector, U::Vector)` — single-band convenience constructor from vectors.
- `HubbardParams(t::Matrix, U::Matrix)` — multi-band constructor from matrices; automatically checks dimensions and Hermiticity.
"""
struct HubbardParams{T<:AbstractFloat}
    bands::Int64
    t::Dict{NTuple{2, Int64}, T}          # t_ii=µ_i, t_ij hopping i→j
    U::Dict{NTuple{4, Int64}, T}          # U_ijkl c⁺_i c⁺_j c_k c_l

    function HubbardParams(bands::Int64, t::Dict{NTuple{2,Int64}, T}, U::Dict{NTuple{4,Int},T}) where {T<:AbstractFloat}
        @assert bands > 0 "Number of bands must be a positive integer"
        new{T}(bands, t, U)
    end
end
# Constructors
function HubbardParams(t::Union{Vector{T}, Matrix{T}}, U::Dict{NTuple{4,Int},T}) where {T<:AbstractFloat}
    bands = isa(t, Matrix) ? size(t,1) : 1
    return HubbardParams(bands, hopping_matrix2dict(t), U)
end
function HubbardParams(t::Vector{T}, U::Vector{T}) where {T<:AbstractFloat}
    interaction = Dict{NTuple{4,Int},T}()
    for (i, val) in enumerate(U)
        addU!(interaction, (1,i,i,1), val)
        addU!(interaction, (i,1,1,i), val)
    end
    return HubbardParams(1, hopping_matrix2dict(t), interaction)
end
function HubbardParams(t::Matrix{T}, U::Matrix{T}) where {T<:AbstractFloat}
    bands = size(t, 1)

    # --- basic checks ---
    @assert size(U, 1) == bands "First dimension of U ($(size(U,1))) must be equal to number of bands ($bands)"
    @assert size(U, 2) % bands == 0 "Second dimension of U ($(size(U,2))) must be multiple of number of bands ($bands)"
    @assert ishermitian(U[1:bands, 1:bands]) "U on-site matrix is not Hermitian"

    interaction = Dict{NTuple{4,Int},T}()
    for i in 1:bands
        for j in 1:size(U,2)
            addU!(interaction, (i,j,j,i), U[i,j])
            j>bands && addU!(interaction, (j,i,i,j), U[i,j])
        end
    end

    return HubbardParams(bands, hopping_matrix2dict(t), interaction)
end


###############
# Extra terms #
###############

abstract type AbstractHamiltonianTerm end

# Add 3-body interaction term and its Hermitian conjugate
function addV!(V::Dict{NTuple{6,Int},T}, key::NTuple{6,Int}, val::T) where {T}
    if val != 0
        V[key] = val
        i,j,k,l,n,m = key
        V[(m,n,l,k,j,i)] = conj(val)          # Hermitian conjugate term
    end
end
# Add standard 3-body interaction terms to dictionary
function dominant_threebody_term(i::Int64, j::Int64, value::T, V::Dict{NTuple{6,Int},T}=Dict{NTuple{6,Int},T}()) where {T<:AbstractFloat}
    addV!(V, (i,i,j,j,i,i), value)
    addV!(V, (i,j,i,i,j,i), value)
    addV!(V, (j,i,i,i,i,j), value)
    addV!(V, (i,j,j,j,j,i), value)
    addV!(V, (j,i,j,j,i,j), value)
    addV!(V, (j,j,i,i,j,j), value)
    return V
end

"""
    ThreeBodyTerm{T<:AbstractFloat} <: AbstractHamiltonianTerm

Represents three-body interactions in the Hamiltonian.

# Fields
- `bands::Int64`  
    Number of bands (orbitals) in the system.
- `V::Dict{NTuple{6,Int}, T}`  
    Three-body interaction amplitudes `c⁺_i c⁺_j c⁺_k c_l c_m c_n`. Zero
    entries can be omitted.

# Constructors
- `ThreeBodyTerm(V::Vector{T})` — single-band constructor from a vector.
- `ThreeBodyTerm(V::Matrix{T})` — multi-band constructor from a matrix.
"""
struct ThreeBodyTerm{T<:AbstractFloat} <: AbstractHamiltonianTerm
    bands::Int64
    V::Dict{NTuple{6,Int}, T}
end
# Constructors
function ThreeBodyTerm(V::Vector{T}) where {T<:AbstractFloat}
    threebody = Dict{NTuple{6,Int},T}()
    for (i, val) in enumerate(V)
        threebody = dominant_threebody_term(1, i+1, val, threebody)
    end

    return ThreeBodyTerm(1, threebody)
end
function ThreeBodyTerm(V::Matrix{T}) where {T<:AbstractFloat}
    bands = size(V, 1)
    @assert size(V, 2) % bands == 0 "Second dimension of V ($(size(V,2))) must be multiple of number of bands ($bands)"
    @assert ishermitian(V[1:bands, 1:bands]) "V on-site matrix is not Hermitian"

    threebody = Dict{NTuple{6,Int},T}()
    for i in 1:bands
        for j in 1:size(V,2)
            threebody = dominant_threebody_term(i, j, V[i,j], threebody)
        end
    end

    return ThreeBodyTerm(bands, threebody)
end

"""
    MagneticField{T<:AbstractFloat} <: AbstractHamiltonianTerm

Represents a magnetic field term in the Hamiltonian `B * Sᶻ`.

# Fields
- `B::T`  
    Magnetic field strength.

# Constructors
- `MagneticField(B)` — creates the term with specified magnetic field strength.
"""
struct MagneticField{T<:AbstractFloat} <: AbstractHamiltonianTerm
    B::T            # Magnetic field strength
end

"""
    StaggeredField{T<:AbstractFloat} <: AbstractHamiltonianTerm

Represents a staggered magnetic field term in the Hamiltonian. 
For multi-band models, the staggering is applied between equivalent orbitals.

# Fields
- `J::T`  
    Inter-chain Hund's coupling.
- `Ms::T`  
    Initial staggered magnetization.

# Constructors
- `StaggeredField(J, Ms)` — creates the term with specified coupling and initial magnetization.
"""
struct StaggeredField{T<:AbstractFloat} <: AbstractHamiltonianTerm
    J::T            # Inter-chain Hund's coupling
    Ms::T           # Initial staggered magnetization
end

"""
    HolsteinTerm{T<:AbstractFloat} <: AbstractHamiltonianTerm

Represents a Holstein-type electron–phonon coupling terms `w b⁺ᵢ bᵢ` and`gₐ(nᵢₐ-<n>)(b⁺ᵢ + bᵢ)` in the Hamiltonian.

# Fields
- `w::Vector{T}`  
    Local phonon frequency.
- `g::Vector{T}`  
    Electron–phonon coupling strength per phonon.
- `max_b::Int64`  
    Maximum number of phonons allowed per site.
- `mean_ne::T`  
    Mean number of electrons in Hubbard model.

# Constructors
- `HolsteinTerm(w, g, max_b, mean_ne)` — creates the term with specified phonon frequency,
  coupling, and phonon truncation.
"""
struct HolsteinTerm{T<:AbstractFloat} <: AbstractHamiltonianTerm
    w::Vector{T}                # Phonon frequency in term `w b⁺ᵢ bᵢ`
    g::Matrix{T}                # Electron-phonon coupling strength per Hubbard band
    max_b::Int64                # Max allowed phonons per site
    mean_ne::T                  # Mean number of electrons in Hubbard model

    function HolsteinTerm(w::Vector{T}, g::Matrix{T}, max_b::Int64, mean_ne::T) where {T<:AbstractFloat}
        @assert max_b > 0 "Max allowed number of phonons must be a positive integer"
        new{T}(w, g, max_b, mean_ne)
    end
end

"""
    Bollmark{T<:AbstractFloat} <: AbstractHamiltonianTerm

Bollmark term used in a perturbative treatment, parameterizing effective interchain/interladder
processes. Ref: Bollmark et al., Phys. Rev. X 13, 011039 (2023)

# Fields
- `alpha::Vector{T}`
    Self-consistent parameters (e.g. mean-field amplitudes) associated with the first Bollmark term.
- `beta::Vector{T}`
    Self-consistent parameters (e.g. mean-field amplitudes) associated with the second Bollmark term.
# Notes
- `alpha` and `beta` are not fixed couplings: they should be iterated to convergence together
  with the ground state (or other target state) to satisfy the chosen self-consistency condition.
"""
struct Bollmark{T<:AbstractFloat} <: AbstractHamiltonianTerm
    alpha::Vector{T}
    beta::Vector{T}
end


######################
# Calculation set up #
######################

# Check for duplicate Hamiltonian terms
function find_duplicate(terms::Tuple)
    for (i, t) in enumerate(terms)
        T = typeof(t)
        for s in terms[i+1:end]
            typeof(s) === T && return T
        end
    end
    return nothing
end

"""
    CalcConfig{T<:AbstractFloat, HamiltonianTerms<:Tuple{Vararg{AbstractHamiltonianTerm}}}

Holds all configuration information for a lattice or many-body calculation,
including symmetries, the base Hubbard Hamiltonian, and optional additional Hamiltonian terms.

# Fields
- `symmetries::SymmetryConfig`  
    Contains particle-number and spin symmetries, unit-cell geometry, and optional filling.
- `hubbard::HubbardParams{T}`  
    Base electronic Hamiltonian parameters (bands, hopping, and two-body interactions).
- `terms::HamiltonianTerms`  
    Tuple of additional Hamiltonian terms (subtypes of `AbstractHamiltonianTerm`).

# Constructors
- `CalcConfig(symmetries, hubbard, terms)` — creates a configuration with specified
  symmetries, Hubbard parameters, and extra terms. Duplicate term types are checked,
  and band consistency is enforced.
- `CalcConfig(symmetries, hubbard, term)` — convenience constructor with one extra term (`terms = (term,)`).
- `CalcConfig(symmetries, hubbard)` — convenience constructor with no extra terms (`terms = ()`).
"""
struct CalcConfig{
    T<:AbstractFloat,
    HamiltonianTerms<:Tuple{Vararg{AbstractHamiltonianTerm}}
}
    symmetries::SymmetryConfig
    hubbard::HubbardParams
    terms::HamiltonianTerms

    function CalcConfig(
                symmetries::SymmetryConfig,
                hubbard::HubbardParams{T},
                terms::HamiltonianTerms
            ) where {T<:AbstractFloat, HamiltonianTerms<:Tuple{Vararg{AbstractHamiltonianTerm}}}

        dup = find_duplicate(terms)
        dup === nothing || error("Duplicate Hamiltonian term detected: $dup")

        bands = hubbard.bands
        for term in terms
            if :bands in fieldnames(typeof(term))
                @assert term.bands == bands "Number of bands in HubbardParams does not match number of bands in $term"
            end
            if term isa HolsteinTerm
                @assert size(term.g,1) == bands "Length of electron-phonon coupling vector does not match number of bands in HubbardParams"
            end
        end

        new{T, HamiltonianTerms}(symmetries, hubbard, terms)
    end
    CalcConfig(
            symmetries::SymmetryConfig, 
            hubbard::HubbardParams{T}, 
            term::AbstractHamiltonianTerm
        ) where {T<:AbstractFloat} = CalcConfig(symmetries, hubbard, (term,))
    CalcConfig(symmetries::SymmetryConfig, hubbard::HubbardParams{T}) where {T<:AbstractFloat} = CalcConfig(symmetries, hubbard, ())
end